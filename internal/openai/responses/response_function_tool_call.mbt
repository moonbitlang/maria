///|
/// A tool call to run a function.
pub struct ResponseFunctionToolCall {
  /// A JSON string of the arguments to pass to the function.
  arguments : String
  /// The unique ID of the function tool call generated by the model.
  call_id : String
  /// The name of the function to run.
  name : String
  /// The unique ID of the function tool call.
  id : String?
  /// The status of the item.
  status : ResponseFunctionToolCallStatus?
}

///|
/// The status of the item.
enum ResponseFunctionToolCallStatus {
  InProgress
  Completed
  Incomplete
}

///|
pub impl ToJson for ResponseFunctionToolCallStatus with to_json(
  self : ResponseFunctionToolCallStatus,
) -> Json {
  match self {
    InProgress => "in_progress"
    Completed => "completed"
    Incomplete => "incomplete"
  }
}

///|
pub impl @json.FromJson for ResponseFunctionToolCallStatus with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseFunctionToolCallStatus raise @json.JsonDecodeError {
  guard json is String(status_str) else {
    raise @json.JsonDecodeError(
      (path, "ResponseFunctionToolCallStatus: expected string"),
    )
  }
  match status_str {
    "in_progress" => InProgress
    "completed" => Completed
    "incomplete" => Incomplete
    _ =>
      raise @json.JsonDecodeError(
        (path, "ResponseFunctionToolCallStatus: invalid status '\{status_str}'"),
      )
  }
}

///|
pub impl ToJson for ResponseFunctionToolCall with to_json(
  self : ResponseFunctionToolCall,
) -> Json {
  let obj : Map[String, Json] = {
    "type": "function_call",
    "arguments": self.arguments,
    "call_id": self.call_id,
    "name": self.name,
  }
  if self.id is Some(id) {
    obj["id"] = id.to_json()
  }
  if self.status is Some(status) {
    obj["status"] = status.to_json()
  }
  Json::object(obj)
}

///|
pub impl @json.FromJson for ResponseFunctionToolCall with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseFunctionToolCall raise @json.JsonDecodeError {
  let object = @jsonx.as_object(json, path~)
  let arguments : String = object.required("arguments", path~)
  let call_id : String = object.required("call_id", path~)
  let name : String = object.required("name", path~)
  let _ : String = object.required("type", path~)
  let id : String? = object.optional("id", path~)
  let status : ResponseFunctionToolCallStatus? = object.optional(
    "status",
    path~,
  )
  ResponseFunctionToolCall::{ arguments, call_id, name, id, status }
}
