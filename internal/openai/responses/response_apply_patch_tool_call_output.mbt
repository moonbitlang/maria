///|
/// The output emitted by an apply patch tool call.
struct ResponseApplyPatchToolCallOutput {
  /// The unique ID of the apply patch tool call output.
  id : String
  /// The unique ID of the apply patch tool call generated by the model.
  call_id : String
  /// The status of the apply patch tool call output.
  status : ResponseApplyPatchToolCallOutputStatus
  /// The ID of the entity that created this tool call output.
  created_by : String?
  /// Optional textual output returned by the apply patch tool.
  output : String?
}

///|
/// The status of the apply patch tool call output.
enum ResponseApplyPatchToolCallOutputStatus {
  Completed
  Failed
}

///|
pub impl ToJson for ResponseApplyPatchToolCallOutputStatus with to_json(
  self : ResponseApplyPatchToolCallOutputStatus,
) -> Json {
  match self {
    Completed => "completed"
    Failed => "failed"
  }
}

///|
pub impl @json.FromJson for ResponseApplyPatchToolCallOutputStatus with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseApplyPatchToolCallOutputStatus raise @json.JsonDecodeError {
  guard json is String(status_str) else {
    raise @json.JsonDecodeError(
      (path, "ResponseApplyPatchToolCallOutputStatus: expected string"),
    )
  }
  match status_str {
    "completed" => Completed
    "failed" => Failed
    _ =>
      raise @json.JsonDecodeError(
        (
          path,
          "ResponseApplyPatchToolCallOutputStatus: invalid status '\{status_str}'",
        ),
      )
  }
}

///|
pub impl ToJson for ResponseApplyPatchToolCallOutput with to_json(
  self : ResponseApplyPatchToolCallOutput,
) -> Json {
  let obj : Map[String, Json] = {
    "type": "apply_patch_call_output",
    "id": self.id,
    "call_id": self.call_id,
    "status": self.status,
  }
  if self.created_by is Some(created_by) {
    obj["created_by"] = created_by.to_json()
  }
  if self.output is Some(output) {
    obj["output"] = output.to_json()
  }
  Json::object(obj)
}

///|
pub impl @json.FromJson for ResponseApplyPatchToolCallOutput with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseApplyPatchToolCallOutput raise @json.JsonDecodeError {
  let object = @jsonx.as_object(json, path~)
  let id : String = object.required("id", path~)
  let call_id : String = object.required("call_id", path~)
  let status : ResponseApplyPatchToolCallOutputStatus = object.required(
    "status",
    path~,
  )
  let _ : String = object.required("type", path~)
  let created_by : String? = object.optional("created_by", path~)
  let output : String? = object.optional("output", path~)
  ResponseApplyPatchToolCallOutput::{ id, call_id, status, created_by, output }
}
