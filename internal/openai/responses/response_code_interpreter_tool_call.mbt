///|
/// A call to the code interpreter tool.
struct ResponseCodeInterpreterToolCall {
  /// The unique ID of the code interpreter tool call.
  id : String
  /// The code to run.
  code : String
  /// The ID of the container used to run the code.
  container_id : String
  /// The outputs generated by the code interpreter.
  outputs : Array[ResponseCodeInterpreterToolCallOutput]
  /// The status of the code interpreter tool call.
  status : ResponseCodeInterpreterToolCallStatus
}

///|
enum ResponseCodeInterpreterToolCallOutput {
  Logs(ResponseCodeInterpreterToolCallOutputLogs)
  Image(ResponseCodeInterpreterToolCallOutputImage)
}

///|
/// The logs output from the code interpreter.
struct ResponseCodeInterpreterToolCallOutputLogs {
  /// The logs output from the code interpreter.
  logs : String
}

///|
/// The image output from the code interpreter.
struct ResponseCodeInterpreterToolCallOutputImage {
  /// The URL of the image output from the code interpreter.
  url : String
}

///|
/// The status of the code interpreter tool call.
enum ResponseCodeInterpreterToolCallStatus {
  InProgress
  Completed
  Incomplete
  Interpreting
  Failed
}

///|
pub impl ToJson for ResponseCodeInterpreterToolCallStatus with to_json(
  self : ResponseCodeInterpreterToolCallStatus,
) -> Json {
  match self {
    InProgress => "in_progress"
    Completed => "completed"
    Incomplete => "incomplete"
    Interpreting => "interpreting"
    Failed => "failed"
  }
}

///|
pub impl @json.FromJson for ResponseCodeInterpreterToolCallStatus with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseCodeInterpreterToolCallStatus raise @json.JsonDecodeError {
  guard json is String(status_str) else {
    raise @json.JsonDecodeError(
      (path, "ResponseCodeInterpreterToolCallStatus: expected string"),
    )
  }
  match status_str {
    "in_progress" => InProgress
    "completed" => Completed
    "incomplete" => Incomplete
    "interpreting" => Interpreting
    "failed" => Failed
    _ =>
      raise @json.JsonDecodeError(
        (
          path,
          "ResponseCodeInterpreterToolCallStatus: invalid status '\{status_str}'",
        ),
      )
  }
}

///|
pub impl ToJson for ResponseCodeInterpreterToolCallOutputLogs with to_json(
  self : ResponseCodeInterpreterToolCallOutputLogs,
) -> Json {
  { "type": "logs", "logs": self.logs }
}

///|
pub impl @json.FromJson for ResponseCodeInterpreterToolCallOutputLogs with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseCodeInterpreterToolCallOutputLogs raise @json.JsonDecodeError {
  let object = @jsonx.as_object(json, path~)
  let logs : String = object.required("logs", path~)
  let _ : String = object.required("type", path~)
  ResponseCodeInterpreterToolCallOutputLogs::{ logs, }
}

///|
pub impl ToJson for ResponseCodeInterpreterToolCallOutputImage with to_json(
  self : ResponseCodeInterpreterToolCallOutputImage,
) -> Json {
  { "type": "image", "url": self.url }
}

///|
pub impl @json.FromJson for ResponseCodeInterpreterToolCallOutputImage with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseCodeInterpreterToolCallOutputImage raise @json.JsonDecodeError {
  let object = @jsonx.as_object(json, path~)
  let _ : String = object.required("type", path~)
  let url : String = object.required("url", path~)
  ResponseCodeInterpreterToolCallOutputImage::{ url, }
}

///|
pub impl ToJson for ResponseCodeInterpreterToolCallOutput with to_json(
  self : ResponseCodeInterpreterToolCallOutput,
) -> Json {
  match self {
    Logs(output) => output.to_json()
    Image(output) => output.to_json()
  }
}

///|
pub impl @json.FromJson for ResponseCodeInterpreterToolCallOutput with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseCodeInterpreterToolCallOutput raise @json.JsonDecodeError {
  let object = @jsonx.as_object(json, path~)
  let type_str : String = object.required("type", path~)
  match type_str {
    "logs" => {
      let output = @json.from_json(json, path~)
      Logs(output)
    }
    "image" => {
      let output = @json.from_json(json, path~)
      Image(output)
    }
    _ =>
      raise @json.JsonDecodeError(
        (
          path.add_key("type"),
          "ResponseCodeInterpreterToolCallOutput: invalid type '\{type_str}'",
        ),
      )
  }
}

///|
pub impl ToJson for ResponseCodeInterpreterToolCall with to_json(
  self : ResponseCodeInterpreterToolCall,
) -> Json {
  {
    "type": "code_interpreter_call",
    "id": self.id,
    "code": self.code,
    "container_id": self.container_id,
    "outputs": self.outputs,
    "status": self.status,
  }
}

///|
pub impl @json.FromJson for ResponseCodeInterpreterToolCall with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseCodeInterpreterToolCall raise @json.JsonDecodeError {
  let object = @jsonx.as_object(json, path~)
  let id : String = object.required("id", path~)
  let code : String = object.required("code", path~)
  let container_id : String = object.required("container_id", path~)
  let outputs : Array[ResponseCodeInterpreterToolCallOutput] = object.required(
    "outputs",
    path~,
  )
  let status : ResponseCodeInterpreterToolCallStatus = object.required(
    "status",
    path~,
  )
  let _ : String = object.required("type", path~)
  ResponseCodeInterpreterToolCall::{ id, code, container_id, outputs, status }
}
