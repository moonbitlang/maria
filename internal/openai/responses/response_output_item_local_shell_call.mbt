///|
/// A tool call to run a command on the local shell.
struct ResponseOutputItemLocalShellCall {
  /// The unique ID of the local shell call.
  id : String
  /// Execute a shell command on the server.
  action : ResponseOutputItemLocalShellCallAction
  /// The unique ID of the local shell tool call generated by the model.
  call_id : String
  /// The status of the local shell call.
  status : ResponseOutputItemLocalShellCallStatus
}

///|
/// Execute a shell command on the server.
struct ResponseOutputItemLocalShellCallAction {
  /// The command to run.
  command : Array[String]
  /// Environment variables to set for the command.
  env : Map[String, String]
  /// Optional timeout in milliseconds for the command.
  timeout_ms : Int64?
  /// Optional user to run the command as.
  user : String?
  /// Optional working directory to run the command in.
  working_directory : String?
}

///|
/// The status of the local shell call.
enum ResponseOutputItemLocalShellCallStatus {
  InProgress
  Completed
  Incomplete
}

///|
pub impl ToJson for ResponseOutputItemLocalShellCallStatus with to_json(
  self : ResponseOutputItemLocalShellCallStatus,
) -> Json {
  match self {
    InProgress => "in_progress"
    Completed => "completed"
    Incomplete => "incomplete"
  }
}

///|
pub impl @json.FromJson for ResponseOutputItemLocalShellCallStatus with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseOutputItemLocalShellCallStatus raise @json.JsonDecodeError {
  guard json is String(status_str) else {
    raise @json.JsonDecodeError(
      (path, "ResponseOutputItemLocalShellCallStatus: expected string"),
    )
  }
  match status_str {
    "in_progress" => InProgress
    "completed" => Completed
    "incomplete" => Incomplete
    _ =>
      raise @json.JsonDecodeError(
        (
          path,
          "ResponseOutputItemLocalShellCallStatus: invalid status '\{status_str}'",
        ),
      )
  }
}

///|
pub impl ToJson for ResponseOutputItemLocalShellCallAction with to_json(
  self : ResponseOutputItemLocalShellCallAction,
) -> Json {
  let obj : Map[String, Json] = {
    "type": "exec",
    "command": self.command,
    "env": self.env,
  }
  if self.timeout_ms is Some(timeout_ms) {
    obj["timeout_ms"] = timeout_ms.to_json()
  }
  if self.user is Some(user) {
    obj["user"] = user.to_json()
  }
  if self.working_directory is Some(working_directory) {
    obj["working_directory"] = working_directory.to_json()
  }
  Json::object(obj)
}

///|
pub impl @json.FromJson for ResponseOutputItemLocalShellCallAction with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseOutputItemLocalShellCallAction raise @json.JsonDecodeError {
  let object = @jsonx.as_object(json, path~)
  let command : Array[String] = object.required("command", path~)
  let env : Map[String, String] = object.required("env", path~)
  let _ : String = object.required("type", path~)
  let timeout_ms : Int64? = object.optional("timeout_ms", path~)
  let user : String? = object.optional("user", path~)
  let working_directory : String? = object.optional("working_directory", path~)
  ResponseOutputItemLocalShellCallAction::{
    command,
    env,
    timeout_ms,
    user,
    working_directory,
  }
}

///|
pub impl ToJson for ResponseOutputItemLocalShellCall with to_json(
  self : ResponseOutputItemLocalShellCall,
) -> Json {
  {
    "type": "local_shell_call",
    "id": self.id,
    "action": self.action,
    "call_id": self.call_id,
    "status": self.status,
  }
}

///|
pub impl @json.FromJson for ResponseOutputItemLocalShellCall with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseOutputItemLocalShellCall raise @json.JsonDecodeError {
  let object = @jsonx.as_object(json, path~)
  let id : String = object.required("id", path~)
  let action : ResponseOutputItemLocalShellCallAction = object.required(
    "action",
    path~,
  )
  let call_id : String = object.required("call_id", path~)
  let status : ResponseOutputItemLocalShellCallStatus = object.required(
    "status",
    path~,
  )
  let _ : String = object.required("type", path~)
  ResponseOutputItemLocalShellCall::{ id, action, call_id, status }
}
