///|
/// A call to a custom tool created by the model.
struct ResponseCustomToolCall {
  /// An identifier used to map this custom tool call to a tool call output.
  call_id : String
  /// The input for the custom tool call generated by the model.
  input : String
  /// The name of the custom tool being called.
  name : String
  /// The unique ID of the custom tool call in the OpenAI platform.
  id : String?
}

///|
pub impl ToJson for ResponseCustomToolCall with to_json(
  self : ResponseCustomToolCall,
) -> Json {
  let obj : Map[String, Json] = {
    "type": "custom_tool_call",
    "call_id": self.call_id,
    "input": self.input,
    "name": self.name,
  }
  if self.id is Some(id) {
    obj["id"] = id.to_json()
  }
  Json::object(obj)
}

///|
pub impl @json.FromJson for ResponseCustomToolCall with from_json(
  json : Json,
  path : @json.JsonPath,
) -> ResponseCustomToolCall raise @json.JsonDecodeError {
  let object = @jsonx.as_object(json, path~)
  let call_id : String = object.required("call_id", path~)
  let input : String = object.required("input", path~)
  let name : String = object.required("name", path~)
  let _ : String = object.required("type", path~)
  let id : String? = object.optional("id", path~)
  ResponseCustomToolCall::{ call_id, input, name, id }
}
